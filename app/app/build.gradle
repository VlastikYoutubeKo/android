buildscript {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath group: 'org.yaml', name: 'snakeyaml', version: '2.4'
    }
}

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.1.20'
    id 'org.jetbrains.kotlin.plugin.compose' version '2.1.20'
}


import java.text.SimpleDateFormat
import org.yaml.snakeyaml.Yaml


def bringyourHomeDir = System.getenv('BRINGYOUR_HOME') ?:
        file("${rootDir.absolutePath}/../..").absolutePath

ext.envSecret = { envName ->
    def servicesFile = file("$bringyourHomeDir/vault/$envName/services.yml")
    if (servicesFile.exists()) {
        def services = new Yaml().load(servicesFile.newInputStream())
        if (services.hidden_prefixes && 0 < services.hidden_prefixes.size()) {
            services.hidden_prefixes[0]
        } else {
            ''
        }
    } else {
        // services file doesn't exist
        println("WARNING: [${envName}] services file doesn't exist, so the env secret won't be set (${servicesFile})")
        ''
    }
}

android {
    ext.appVersion = { ->
        def warpVersion = ''
        if (new File("${rootDir.absolutePath}/local.properties").exists()) {
            Properties localProperties = new Properties()
            localProperties.load(new File("${rootDir.absolutePath}/local.properties").newDataInputStream())
            warpVersion = localProperties.getProperty('warp.version')
        }

        if (warpVersion) {
            warpVersion
        } else {
            def osName = ''
            def osArch = ''
            switch (System.getProperty('os.name')) {
                case 'Linux':
                    osName = 'linux'
                    break
                case 'Mac OS X':
                    osName = 'darwin'
                    break
            }
            switch (System.getProperty('os.arch')) {
                case 'amd64':
                    osArch = 'amd64'
                    break
                case 'aarch64':
                    osArch = 'arm64'
                    break
            }

            def b = new ProcessBuilder(
                    "${bringyourHomeDir}/warp/warpctl/build/${osName}/${osArch}/warpctl",
                    'ls',
                    'version'
            )
            b.redirectErrorStream()
            def p = b.start()
            new String(p.inputStream.readAllBytes()).trim()
        }
    }
    ext.appVersionCode = { ->
        def warpVersionCode = ''
        if (new File("${rootDir.absolutePath}/local.properties").exists()) {
            Properties localProperties = new Properties()
            localProperties.load(new File("${rootDir.absolutePath}/local.properties").newDataInputStream())
            warpVersionCode = localProperties.getProperty('warp.version_code')
        }

        if (warpVersionCode) {
            warpVersionCode as Integer
        } else {
            // hours since company founding
            def companyFounded = new SimpleDateFormat('MM/dd/yyyy').parse('05/23/2023')
            ((System.currentTimeMillis() - companyFounded.getTime()) / (1000 * 60 * 60)) as Integer
        }
    }

    namespace 'com.bringyour.network'
    compileSdk 35
    ndkVersion '28.0.13004108'

    defaultConfig {
        applicationId "com.bringyour.network"
        minSdk 24
        targetSdk 35
        versionCode appVersionCode()
        versionName appVersion()

        setProperty('archivesBaseName', "${applicationId}-${versionName}-${versionCode}")

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        // see https://developer.android.com/build/shrink-code
        ndk {
            debugSymbolLevel 'SYMBOL_TABLE'
        }

    }

    signingConfigs {
        debug {
            storeFile file("${System.properties['user.home']}/.android/debug.keystore")
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storePassword 'android'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions += 'version'

    // BuildConfig.FLAVOR
    productFlavors {
        github {
            isDefault true
            dimension = 'version'
            applicationId 'com.bringyour.network'
//            versionNameSuffix '-github'

            buildConfigField "String", "BRINGYOUR_BUNDLE_HOST_NAME", '"ur.network"'
            buildConfigField "String", "BRINGYOUR_BUNDLE_ENV_NAME", '"main"'
            buildConfigField "String", "BRINGYOUR_BUNDLE_ENV_SECRET", 'null'
            buildConfigField "boolean", "BRINGYOUR_BUNDLE_NET_EXPOSE_SERVER_IPS", 'true'
            buildConfigField "boolean", "BRINGYOUR_BUNDLE_NET_EXPOSE_SERVER_HOST_NAMES", 'true'
            buildConfigField "String", "BRINGYOUR_BUNDLE_LINK_HOST_NAME", '"ur.io"'
            // the migratioon host name is used for services
            // TODO this will be removed once we fully migrate to ur.network
            buildConfigField "String", "BRINGYOUR_BUNDLE_MIGRATION_HOST_NAME", '"bringyour.com"'
            buildConfigField "String", "BRINGYOUR_BUNDLE_STORE", 'null'
            buildConfigField "String", "BRINGYOUR_BUNDLE_WALLET", '"circle"'
            // hard coded initial extender networks
            // this can avoid the need for an initial doh query
            // json string array
            buildConfigField "String[]", "BRINGYOUR_BUNDLE_NET_EXTENDER_NETWORKS", 'new String[]{}'
            buildConfigField "boolean", "BRINGYOUR_BUNDLE_SSO_GOOGLE", 'false'
        }
        play {
            dimension = 'version'
            applicationId 'com.bringyour.network'
            versionNameSuffix '-play'

            buildConfigField "String", "BRINGYOUR_BUNDLE_HOST_NAME", '"ur.network"'
            buildConfigField "String", "BRINGYOUR_BUNDLE_ENV_NAME", '"main"'
            buildConfigField "String", "BRINGYOUR_BUNDLE_ENV_SECRET", 'null'
            buildConfigField "boolean", "BRINGYOUR_BUNDLE_NET_EXPOSE_SERVER_IPS", 'true'
            buildConfigField "boolean", "BRINGYOUR_BUNDLE_NET_EXPOSE_SERVER_HOST_NAMES", 'true'
            buildConfigField "String", "BRINGYOUR_BUNDLE_LINK_HOST_NAME", '"ur.io"'
            buildConfigField "String", "BRINGYOUR_BUNDLE_MIGRATION_HOST_NAME", '"bringyour.com"'
            buildConfigField "String", "BRINGYOUR_BUNDLE_STORE", '"play"'
            buildConfigField "String", "BRINGYOUR_BUNDLE_WALLET", '"circle"'
            buildConfigField "String[]", "BRINGYOUR_BUNDLE_NET_EXTENDER_NETWORKS", 'new String[]{}'
            buildConfigField "boolean", "BRINGYOUR_BUNDLE_SSO_GOOGLE", 'true'
        }
        solana_dapp {
            dimension = 'version'
            applicationId 'com.bringyour.network'
            versionNameSuffix '-solana_dapp'

            buildConfigField "String", "BRINGYOUR_BUNDLE_HOST_NAME", '"ur.network"'
            buildConfigField "String", "BRINGYOUR_BUNDLE_ENV_NAME", '"main"'
            buildConfigField "String", "BRINGYOUR_BUNDLE_ENV_SECRET", 'null'
            buildConfigField "boolean", "BRINGYOUR_BUNDLE_NET_EXPOSE_SERVER_IPS", 'true'
            buildConfigField "boolean", "BRINGYOUR_BUNDLE_NET_EXPOSE_SERVER_HOST_NAMES", 'true'
            buildConfigField "String", "BRINGYOUR_BUNDLE_LINK_HOST_NAME", '"ur.io"'
            buildConfigField "String", "BRINGYOUR_BUNDLE_MIGRATION_HOST_NAME", '"bringyour.com"'
            buildConfigField "String", "BRINGYOUR_BUNDLE_STORE", 'null'
            buildConfigField "String", "BRINGYOUR_BUNDLE_WALLET", '"solana"'
            buildConfigField "String[]", "BRINGYOUR_BUNDLE_NET_EXTENDER_NETWORKS", 'new String[]{}'
            buildConfigField "boolean", "BRINGYOUR_BUNDLE_SSO_GOOGLE", 'true'
        }
    }

    sourceSets {
        github {
            java {
                srcDirs += ['src/ungoogle/java']
            }
        }
        play {
            java {
                srcDirs += ['src/google/java']
            }
        }
        solana_dapp {
            java {
                srcDirs += ['src/google/java']
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    // see https://developer.android.com/reference/tools/gradle-api/7.4/com/android/build/api/dsl/BuildFeatures
    buildFeatures {
        viewBinding true
        buildConfig true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.13"
    }

    tasks.register('goclientBuild', Exec) {
        workingDir "${bringyourHomeDir}/sdk"
        commandLine 'make', 'init', 'build_android'
        environment 'WARP_VERSION', appVersion()
    }

    tasks.withType(JavaCompile).tap {
        configureEach {
            compileTask -> compileTask.dependsOn goclientBuild
        }
    }
}

// local URnetwork SDK
dependencies {
    compileOnly fileTree(dir: "${bringyourHomeDir}/sdk/build/android", include: ['*-sources.jar'])
    implementation fileTree(dir: "${bringyourHomeDir}/sdk/build/android", include: ['*.aar', '*.jar'])
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'

    // serialization
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.8.0")

    // for QR Code generation
    implementation 'com.google.zxing:core:3.5.3'
    implementation 'androidx.biometric:biometric-ktx:1.4.0-alpha02'

    // hilt
    implementation 'com.google.dagger:hilt-android:2.56'
    kapt 'com.google.dagger:hilt-compiler:2.56'
    implementation 'androidx.hilt:hilt-navigation-compose:1.2.0'
}

// compose
dependencies {
    implementation 'androidx.core:core-ktx:1.15.0'

    // jetpack compose
    def composeBom = platform('androidx.compose:compose-bom:2025.03.00')
    implementation composeBom
    androidTestImplementation composeBom

    implementation("androidx.navigation:navigation-compose:2.8.9")
    implementation "androidx.activity:activity-compose:1.10.1"

    // Material Design 3
    implementation 'androidx.compose.material3:material3:1.3.1'
    implementation 'androidx.compose.material3:material3-adaptive-navigation-suite-android:1.3.1'
    implementation 'androidx.compose.material3:material3-window-size-class-android:1.3.1'

    // UI Tests
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.7.8'
    debugImplementation 'androidx.compose.ui:ui-test-manifest:1.7.8'

    // Android Studio Preview support
    implementation 'androidx.compose.ui:ui-tooling-preview:1.7.8'
    debugImplementation 'androidx.compose.ui:ui-tooling:1.7.8'
}


// solana saga
dependencies {
    implementation("com.solanamobile:mobile-wallet-adapter-clientlib-ktx:2.0.6")
    implementation("com.solanamobile:web3-solana:0.2.5")
    implementation("com.solanamobile:rpc-core:0.2.8")
    implementation("io.github.funkatronics:multimult:0.2.3")
}

dependencies {
    githubImplementation 'org.jetbrains.kotlin:kotlin-reflect:2.1.20'
}

// circle wallet
dependencies {
/*build:google*/    playImplementation 'circle.programmablewallet:sdk:1.0.1059'
}

// google play
dependencies {
/*build:google*/    playImplementation 'com.google.android.gms:play-services-auth:21.3.0'
/*build:google*/    playImplementation 'com.android.installreferrer:installreferrer:2.2'

    // play store review
/*build:google*/    playImplementation 'com.google.android.play:review:2.0.2'
/*build:google*/    playImplementation 'com.google.android.play:review-ktx:2.0.2'

    // see https://developer.android.com/google/play/billing/integrate#groovy
/*build:google*/    playImplementation "com.android.billingclient:billing:7.1.1"
/*build:google*/    playImplementation "com.android.billingclient:billing-ktx:7.1.1"
}



// circle wallet
dependencies {
/*build:google*/    solana_dappImplementation 'circle.programmablewallet:sdk:1.0.1059'
}

// google play
dependencies {
/*build:google*/    solana_dappImplementation 'com.google.android.gms:play-services-auth:21.3.0'
/*build:google*/    solana_dappImplementation 'com.android.installreferrer:installreferrer:2.2'

    // play store review
/*build:google*/    solana_dappImplementation 'com.google.android.play:review:2.0.2'
/*build:google*/    solana_dappImplementation 'com.google.android.play:review-ktx:2.0.2'

    // see https://developer.android.com/google/play/billing/integrate#groovy
/*build:google*/    solana_dappImplementation "com.android.billingclient:billing:7.1.1"
/*build:google*/    solana_dappImplementation "com.android.billingclient:billing-ktx:7.1.1"
}



// Allow references to generated code
kapt {
    correctErrorTypes = true
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}
